// Advent of Code - Day 3 Part 1 (Lobby)
// Input: examples/io/aoc_day3.txt

fn max2(a, b) {
    if a > b { a } else { b }
}

fn digit_at(line, idx) {
    parse_int(substring(line, idx, idx + 1))
}

fn max_bank_scan(line, idx, best_right, best_pair) {
    if idx < 0 {
        best_pair
    } else {
        let d = digit_at(line, idx)
        let pair_here = if best_right < 0 { best_pair } else { max2(best_pair, d * 10 + best_right) }
        let next_right = max2(best_right, d)
        max_bank_scan(line, idx - 1, next_right, pair_here)
    }
}

fn max_bank_value(line) {
    let n = len(line)
    if n < 2 { 0 } else { max_bank_scan(line, n - 1, -1, 0) }
}

let lines = read_lines("examples/io/aoc_day3.txt")
let non_empty = [trim(line) | line <- lines, trim(line) != ""]
let bests = [max_bank_value(line) | line <- non_empty]
print(sum(bests))
