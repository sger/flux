// Advent of Code - Day 1 (timed)
// Uses input file: examples/io/aoc_day1.txt

fun wrap_100(value) {
    let reduced = value % 100
    if reduced < 0 {
        reduced + 100
    } else {
        reduced
    }
}

fun parse_rotation(line) {
    let dir = substring(line, 0, 1)
    let distance = parse_int(substring(line, 1, len(line)))
    if dir == "L" {
        0 - distance
    } else {
        distance
    }
}

fun parse_line(line, rest, acc) {
    let cleaned = trim(line)
    if cleaned == "" {
        parse_rotations(rest, acc)
    } else {
        parse_rotations(rest, [parse_rotation(cleaned) | acc])
    }
}

fun parse_rotations(lines, acc) {
    match lines {
        [line | rest] -> parse_line(line, rest, acc),
        _ -> reverse(acc),
    }
}

fun process_step(step, rest, pos, hits) {
    let next_pos = wrap_100(pos + step)
    if next_pos == 0 {
        solve_steps(rest, next_pos, hits + 1)
    } else {
        solve_steps(rest, next_pos, hits)
    }
}

fun solve_steps(steps, pos, hits) {
    match steps {
        [step | rest] -> process_step(step, rest, pos, hits),
        _ -> hits,
    }
}

fun run_day1() {
    let lines = to_list(read_lines("examples/io/aoc_day1.txt"))
    let steps = parse_rotations(lines, None)
    solve_steps(steps, 50, 0)
}

let t0 = now_ms()
let elapsed_via_time = time(run_day1)
let password = run_day1()
let t1 = now_ms()

print("AoC Day 1 password:")
print(password)
print("elapsed (time builtin, ms):")
print(elapsed_via_time)
print("elapsed (now_ms delta, ms):")
print(t1 - t0)
