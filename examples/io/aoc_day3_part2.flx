// Advent of Code - Day 3 Part 2 (Lobby)
// Input: examples/io/aoc_day3.txt

fn digit_at(line, idx) {
    parse_int(substring(line, idx, idx + 1))
}

fn best_digit_pos(line, start, end_inclusive, best_digit, best_pos) {
    if start > end_inclusive {
        best_pos
    } else {
        let d = digit_at(line, start)
        if d > best_digit {
            best_digit_pos(line, start + 1, end_inclusive, d, start)
        } else {
            // Keep earliest position on ties.
            best_digit_pos(line, start + 1, end_inclusive, best_digit, best_pos)
        }
    }
}

fn max_k_value(line, start, k, acc) {
    if k == 0 {
        acc
    } else {
        let n = len(line)
        let end = n - k
        let pos = best_digit_pos(line, start, end, -1, start)
        let d = digit_at(line, pos)
        max_k_value(line, pos + 1, k - 1, acc * 10 + d)
    }
}

fn bank_value_k12(line) {
    let n = len(line)
    if n < 12 { 0 } else { max_k_value(line, 0, 12, 0) }
}

let lines = read_lines("examples/io/aoc_day3.txt")
let non_empty = [trim(line) | line <- lines, trim(line) != ""]
let bank_values = [bank_value_k12(line) | line <- non_empty]
print(sum(bank_values))
